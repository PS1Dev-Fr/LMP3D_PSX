	.global vu1Triangle_CodeStart
	.global vu1Triangle_CodeEnd

; VF 0 /
; VF 01-04 Matrix Projection
; VF 05 in number faces/vertex

; VF 08-11 in vertex
; VF 12-15 out vertex Matrix (or TextureCoord for index)
; VF 16-19 out vertex float

; VF 20-23 out vertex fixed point (index)
; VF 24-27 out vertex float origin zero (index)
; VF 29 index
; VF 30 ,31 clipping

vu1Triangle_CodeStart:
	.p2align 4
	.vu
	
.org 0x00
	nop                             b PS2_VU_Func_Vectrex
	nop                             nop
.org 0x10
	nop                             b PS2_VU_Func_Vectrex_Loop
	nop                             nop
.org 0x20
	nop                             b PS2_VU_Func_Index
	nop                             nop
	
	
;--------------------------------------------------------------------------------
PS2_VU_Func_Vectrex:
	nop                             lq VF05, 5(VI00)
	nop                             lq VF01, 0(VI00)
	nop                             iaddiu VI12, VI00, 518
	nop                             iaddiu VI06, VI00, 6
	nop                             iaddiu VI02, VI00, 6
	nop                             mtir VI04, VF05w
	nop		                        mtir VI14, VF05z


vu1Triangle_double:
	nop                             lqi  VF08, (VI06++)   ; v1
	nop                             iaddiu VI05,VI00, 2048
	nop                             lq VF01, 1(VI00)
	nop                             lq VF02, 2(VI00)
	itof4 VF08, VF08                lq VF03, 3(VI00)
	nop                             lq VF04, 4(VI00)

	nop                             lqi VF29,(VI12++)
	nop		                        iaddiu VI10, VI00, 73
	mulax ACC, VF01, VF08x          lqi  VF09, (VI06++)   ; v2
	madday ACC, VF02, VF08y         nop
	maddaz ACC, VF03, VF08z         nop
	maddw VF12, VF04, VF08w         iaddiu VI03, VI00, 0

	itof4 VF09, VF09                lqi  VF10, (VI06++)   ; v3
	nop                             mfir.w VF31,VI05
	itof0.w VF31, VF31              iaddiu VI05,VI00, 1024
	itof4 VF10, VF10                mfir.w VF30,VI05
	itof0.w VF30, VF30              iaddiu VI11, VI00, 876
	
vertexLoop:
	;Phase1 
	mulax ACC, VF01, VF09x          div q, VF00w, VF12w 

	madday ACC, VF02, VF09y         lqi  VF11, (VI06++)   ; v4
	maddaz ACC, VF03, VF09z         nop
	maddw VF13, VF04, VF09w         nop
	nop                             nop
	itof4 VF11, VF11                sq VF19, 0(VI02)   ; s v4
	mulax ACC, VF01, VF10x          nop

	;Phase2
	mulq.xyz VF16, VF12, q          div q, VF00w, VF13w 

	madday ACC, VF02, VF10y         lqi  VF08, (VI06++)   ; v1
	maddaz ACC, VF03, VF10z         nop
	maddw VF14, VF04, VF10w         nop
	nop                             nop
	itof4 VF08, VF08                nop
	mulax ACC, VF01, VF11x          nop
	
	;Phase3
	mulq.xyz VF17, VF13, q          div q, VF00w, VF14w 

	madday ACC, VF02, VF11y         lqi  VF09, (VI06++)   ; v2
	maddaz ACC, VF03, VF11z         sqi  VF16, (VI02++)   ; s v1
	maddw VF15, VF04, VF11w         nop
	nop                             nop
	itof4 VF09, VF09                nop
	nop                             nop

	;Phase4
	mulq.xyz VF18, VF14, q          div q, VF00w, VF15w 

	mulax ACC, VF01, VF08x          lqi  VF10,(VI06++)   ; v3
	madday ACC, VF02, VF08y         sqi  VF17, (VI02++)   ; s v2
	maddaz ACC, VF03, VF08z         nop
	maddw VF12, VF04, VF08w         nop
	itof4 VF10, VF10                nop
	nop                             sqi VF18, (VI02++)   ; s v3
	
	mulq.xyz VF19, VF15, q          iaddi VI04, VI04, -1

	nop                             nop
	nop                             nop

	;End
	nop                             ibne VI04, VI00, vertexLoop
	nop                             sqi VF19, (VI02++)   ; s v4

vertexLoopOut:

	nop[E]                          nop
	nop                             nop
	
;--------------------------------------------------------------------------------
PS2_VU_Func_Vectrex_Loop:

	nop                             iaddiu VI12, VI00, 518+178
	nop                             mtir VI04, VF05y
	nop		                        mtir VI14, VF05x

	nop                             iaddiu VI02, VI00, 262
	nop                             b vu1Triangle_double
	nop                             iaddiu VI06, VI00, 262

	nop[E]                          nop
	nop                             nop

PS2_VU_Func_Index:

	nop		                        mtir VI07, VF29x
	nop		                        mtir VI08, VF29y
	nop		                        mtir VI09, VF29z
	
	nop		                        lq  VF12, 134(VI07)   ; vt1
	nop		                        lq  VF13, 134(VI08)   ; vt2
	nop		                        lq  VF14, 134(VI09)   ; vt3
	
	nop                             iadd VI02, VI11, VI03
	nop                             iaddiu VI04, VI02, 1
	nop                             iaddiu VI05, VI00, 8
	
;--------------------------------------------------------------------------------
IndexLoop:
	
	itof4 VF12, VF12                lq  VF08, 6(VI07)   ; v1
	itof4 VF13, VF13                lq  VF09, 6(VI08)   ; v2
	itof4 VF14, VF14                lq  VF10, 6(VI09)   ; v3
	nop                             iaddi VI05, VI05, -1

	ftoi4.xyz VF20, VF08           	fcset 0
	ftoi4.xyz VF21, VF09           	iaddi VI14, VI14, -1
	ftoi4.xyz VF22, VF10           	lqi VF29,(VI12++)
	nop		                        sq  VF12, 0(VI04)   ; s vt1

	subw.xyz VF24,VF08,VF31         sq  VF13, 3(VI04)   ; s vt2
	subw.xyz VF25,VF09,VF31         sq  VF14, 6(VI04)   ; s vt3
	subw.xyz VF26,VF10,VF31         mtir VI07, VF29x
	clipw.xyz VF00,VF00             mtir VI08, VF29y

	clipw.xyz VF24,VF30             mtir VI09, VF29z
	clipw.xyz VF25,VF30             sq  VF20, 2(VI04)   ; s v1
	clipw.xyz VF26,VF30             sq  VF21, 5(VI04)   ; s v2
	nop		                        sq  VF22, 8(VI04)   ; s v3
	nop		                        lq  VF12, 134(VI07)   ; vt1
	nop		                        lq  VF13, 134(VI08)   ; vt2

	
	nop                             fcand VI01,0xFFFFFF
	nop                             ibeq VI01, VI00, RNextIndexLoop1
	nop                             nop
	nop                             sq  VF22, 2(VI04)   ; s v1
	nop                             sq  VF22, 5(VI04)   ; s v2
RNextIndexLoop1:

	nop                             ibne VI05, VI00, RNextIndexLoop2
	nop                             iaddiu VI04, VI04, 9
	nop                             xgkick VI02
	
	nop                             iaddiu VI03, VI03, 73
	nop                             iand VI03,VI03,VI10
	
	nop                             iadd VI02, VI11, VI03
	nop                             iaddiu VI04, VI02, 1
	nop                             iaddiu VI05, VI00, 8

RNextIndexLoop2:
	nop                             ibne VI14, VI00, IndexLoop
	nop		                        lq  VF14, 134(VI09)   ; vt3
	
	nop                             nop
	nop[E]                          nop
	nop                             nop
	

;--------------------------------------------------------------------------------
IndexLoop2:
	
	nop		                        lq  VF14, 134(VI09)   ; vt3
	nop                             iadd VI02, VI11, VI03

	itof4 VF12, VF12                lq  VF08, 6(VI07)   ; v1
	itof4 VF13, VF13                lq  VF09, 6(VI08)   ; v2
	itof4 VF14, VF14                lq  VF10, 6(VI09)   ; v3
	nop                             iaddiu VI03, VI03, 73

	ftoi4.xyz VF20, VF08           	fcset 0
	ftoi4.xyz VF21, VF09           	iaddi VI14, VI14, -1
	ftoi4.xyz VF22, VF10           	lqi VF29,(VI12++)
	nop		                        sq  VF12, 1+0(VI02)   ; s vt1

	subw.xyz VF24,VF08,VF31         sq  VF13, 1+3(VI02)   ; s vt2
	subw.xyz VF25,VF09,VF31         sq  VF14, 1+6(VI02)   ; s vt3
	subw.xyz VF26,VF10,VF31         mtir VI07, VF29x
	clipw.xyz VF00,VF00             mtir VI08, VF29y

	clipw.xyz VF24,VF30             mtir VI09, VF29z
	clipw.xyz VF25,VF30             sq  VF20, 1+2(VI02)   ; s v1
	clipw.xyz VF26,VF30             sq  VF21, 1+5(VI02)   ; s v2
	nop		                        sq  VF22, 1+8(VI02)   ; s v3
	nop		                        lq  VF12, 134(VI07)   ; vt1
	nop		                        lq  VF13, 134(VI08)   ; vt2

	
	nop                             fcand VI01,0xFFFFFF
	nop                             ibeq VI01, VI00, RNextIndexLoop12
	nop                             iand VI03,VI03,VI10
	nop                             sq  VF22, 1+2(VI02)   ; s v1
	nop                             sq  VF22, 1+5(VI02)   ; s v2
RNextIndexLoop12:

	nop                             ibne VI14, VI00, IndexLoop2
	nop                             xgkick VI02
	
	nop                             nop
	nop[E]                          nop
	nop                             nop
	



;--------------------------------------------------------------------------------
.org 0x2000
vu1Triangle_CodeEnd:
